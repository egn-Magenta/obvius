#!/usr/bin/perl -w
# $Id$

use strict;
use warnings;
use locale;

use File::Find;
use File::Path;

use File::Spec::Functions qw(abs2rel);

my $base = 'perl';
my $blib = 'perl_blib';

chdir $ARGV[0] if (defined $ARGV[0] and -d $ARGV[0]);

die "No ./$base/ dir in CWD\n" unless (-d $base);

rmtree $blib if (-d $blib);
mkdir $blib, 0755;

my @perl_pm_files;
find(sub {
    push @perl_pm_files, $File::Find::name if (/\.pm$/ and -f $_);
}, $base);

for (@perl_pm_files) {
    my $src = $_;

    my $dest = $_;
    $dest =~ s!/([^/]+)/\1\.pm$!/$1.pm!;
    $dest =~ s!^$base/!$blib/!;

    my $dir = $dest;
    $dir =~ s!^(.*)/[^/]+$!$1!;

    $src = abs2rel($src, $dir);

    unless (-d $dir) {
	mkpath $dir or  warn "mkpath $dir failed: $!\n";
    }
    
    unlink $dest;
    symlink $src, $dest or warn "symlink $src, $dest failed: $!\n";
}
