#!/usr/bin/env perl

use strict;
use warnings;

use Data::Dumper;
use Obvius::Config;
use Obvius;

my $obvius = Obvius->new(
    Obvius::Config->new('ku'), # config
    undef, #user
    undef, #password
    undef, #doctypes
    undef, #fieldtypes
    undef, #fieldspecs
    log => new Obvius::Log("warn")
);
my @fields = ("rightboxes", "boxes1", "boxes2", "boxes3");
my @entities = ({ name => "Calendar",
		  table => "calendars",
		  types => ["Arrangementsliste", "NyArrangementsliste"]},
		{ name => "Newslist",
		  table => "newslists",
		  types => ["Nyhedsliste", "NyNyhedsliste"]});

sub fetch_docids {
  my ($types) = @_;

  my $in_arg = join ',',(("?") x @$types);

  my $docids = $obvius->execute_select("select v.docid as docid from versions v join doctypes dt on (v.type = dt.id) 
                                        where dt.name in ($in_arg) and v.public = 1", @$types);

  my @docids = map { $_->{docid} } @$docids;

  return \@docids;
}

sub get_docids {
    my ($field, $docids_in) = @_;
    
    my @exprs;
    for my $d (@$docids_in) {
	push @exprs, map { "$_:/$d.docid" } (0..8);
    }
    
    my @result;
    while (my @docs = splice @exprs, 0, 1000) { 
	my $question = (join ",", (("?") x @docs));
	my $docids = $obvius->execute_select("select v.docid docid from versions v join vfields vf on (v.docid = vf.docid and v.version = vf.version)  where v.public = 1 AND vf.name = ? and vf.text_value in ($question)", $field, @docs);
	push @result, map { $_->{docid} } @$docids;
    }
    
    return \@result;
}

sub insert {
  my ($table, $docids) = @_;
  
  $obvius->db_begin;
  eval { 
      $obvius->execute_command("delete from $table");
      $obvius->execute_command("insert ignore into $table (docid) values (?)", [ map { [ $_ ] } @$docids ] );
  };
  if ($@) {
      $obvius->db_rollback;
   
      die $@;
  } else {
      $obvius->db_commit;
  }
}

for my $entity (@entities) {
	$entity->{docids} = fetch_docids($entity->{types});
	my $docids = [];
	for my $field (@fields) {
	    push @$docids, @{get_docids($field, $entity->{docids})};
	}
	insert($entity->{table}, $docids);
}
